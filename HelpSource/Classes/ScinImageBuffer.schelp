TITLE:: ScinImageBuffer
summary:: Represents a server-side graphics memory region for sampling static images.
categories:: Quarks>Scintillator>Media
related:: Classes/VTexPos, Classes/VSampler

DESCRIPTION::
The ScinImageBuffer class is designed to be analagous to the SuperCollider audio link::Classes/Buffer:: class, but for reading static images for sampling. ScinImageBuffers are most commonly used inside of link::Classes/ScinthDef:: functions as arguments to link::Classes/VSampler:: instances. Currently ScinImageBuffer support is limited to reading static images, but more may be added in the future.

CLASSMETHODS::

METHOD:: read
Attempts to open the image file at the provided path and read the metadata. If that is successful the server will decode the image file, optionally resize it to the provided width and height, and upload it to the graphics hardware so it is ready for use.

The strong::width:: and strong::height:: arguments are both by default -1, which is an instruction to the server to respect the original dimensions of the image. If only one of the dimension arguments is supplied by -1, the server will scale the image to the non-negative dimension and then preserve the aspect ratio of the image by scaling the negative dimension to the appropriate size. Of course, if both dimensions are non-negative then the server will scale the image to the provided dimensions, disregarding the aspect ratio of the input image.

For example, for a source image that is 200 pixels wide and 100 pixels tall:

table::
## strong::width requested:: || strong::height requested:: || strong::buffer width:: || strong::buffer height:: || strong::notes::
## 400 || 100 || 400 || 100 || Server will disregard aspect ratio of source image if both requested dimensions are nonnegative.
## -1 || 50 || 100 || 50 || In order to maintain 2:1 aspect ratio server has computed a width of 50 px.
## 400 || -1 || 400 || 200 || In order to maintain 2:1 aspect ratio server has computed a height of 400 px.
## -1 || -1 || 200 || 100 || Server has allocated width and height of source image.
::

ARGUMENT:: server
The link::Classes/ScinServer:: on which to read the image and allocate the buffer. If nil, will use code::ScinServer.default::.

ARGUMENT:: path
A string containing the path to the image file to read.

ARGUMENT:: width
An optional integer describing a desired width to scale the image to, or -1.

ARGUMENT:: height
An optional integer describing a desired height to scale the image to, or -1.

ARGUMENT:: action
An optional function to be evaluated once the image has been decoded, uploaded, and this ImageBuffer's instance variables have been updated. The function will be passed this ImageBuffer as an argument.

ARGUMENT:: bufnum
An explicitly specified buffer number. While buffer numbers for ImageBuffer are set on the client, and not allocated on the server, any load or delete operations on an ImageBuffer will clobber any exising ImageBuffer with the same buffer number. So, like link::Classes/Buffer::, the best practice is to leave this unspecified.

INSTANCEMETHODS::
private:: asVGenInput, bindQuery, allocRead

METHOD:: width
Returns the width of the ImageBuffer in pixels.

METHOD:: server
The link::Classes/ScinServer:: that owns the associated buffer.

METHOD:: bufnum
The integer buffer number that uniquely identifies this buffer on the server.

METHOD:: height
Returns the height of the ImageBuffer in pixels.

EXAMPLES::

code::
// Creates a chromakey-like effect using the VecMix VGen to choose between two different
// image buffers based on distance of the first image from the provided target rgb color.
(
~v = ScinServer.new;
~v.options.width = 400;
~v.options.height = 300;
~v.boot;
)

(
~molly = ScinImageBuffer.read(path: "~/src/TestGoldImages/sourceMedia/molly.png".standardizePath);
~storm = ScinImageBuffer.read(path: "~/src/TestGoldImages/sourceMedia/storm.png".standardizePath);
)

(
~f = ScinthDef.new(\chromaKey, { |r, g, b, key = 0.25|
	var m = VSampler.pr(~molly, VTexPos.pr);
	var s = VSampler.pr(~storm, VTexPos.pr);
	var dist = VLength.pr(m - VVec4.pr(r, g, b, 1.0));
	var pick = VStep.pr(key, dist);
	VMix.pr(s, m, pick);
}).add;
)

(
~t = Scinth.new(\chromaKey);
)
::

image::ScinImageBufferA.png::

code::
// Now vary the parameters of the ScinthDef to pick up a brighter color and with a larger key
// threshold.
(
~t.set(\r, 0.64, \g, 0.64, \b, 0.64, \key, 0.5);
)
::

image::ScinImageBufferB.png::
