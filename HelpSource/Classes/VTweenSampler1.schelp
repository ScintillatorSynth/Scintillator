TITLE:: VTweenSampler1
summary:: Provides time-invariant sampling of 1D ScinTween curves
categories:: Quarks>Scintillator>VGens>Tweens
related:: Classes/ScinTween, Classes/VTweenSampler2, Classes/VTweenSampler4, Classes/VTweenGen1

DESCRIPTION::
The VTweenSampler1 class provides a means to sample arbitrary values from a one-dimensional ScinTween table as a VGen.

CLASSMETHODS::
private:: prAddTween

METHOD:: fr
METHOD:: pr
METHOD:: sr

Make a VTweenSampler1 at the requested rate.

ARGUMENT:: tween
The link::Classes/ScinTween:: curve to sample.

ARGUMENT:: t
Where in the curve to sample. These values are in units of time on the ScinTween object, with time starting at 0 and extending to the total duration of the tween. Values of t outside of that range will clamp to the start and end levels in the tween if the tween does not loop, and will repeat if the tween does loop.

returns::
The value of the tween at time t.

strong::dimensions::
table::
## strong::input:: || strong::output::
## tween, 1 || 1
::

INSTANCEMETHODS::
private:: hasTweenVGen, inputDimensions, tween, outputDimensions

EXAMPLES::

code::
(
~o = ScinServerOptions.new;
~v = ScinServer.new(~o).boot;
)

(
~k = ScinthDef.new(\tw, {
	var tween = ScinTween.new(
		levels: #[0, 2pi],
		times: #[1],
		curves: \elasticIn,
		loop: false);
	var pos = VNormPos.pr;
	var r = VLength.pr(pos);
	var v = VTweenSampler1.pr(tween, r);
	var x = r * v.sin;
	var y = r * v.cos;
	VBWOut.pr(VStep.pr(0.9, VLength.pr(pos - VVec2.pr(x, y))));
}).add;
)

(
~t = Scinth.new(\tw);
)
::

image::VTweenSampler1.png::
