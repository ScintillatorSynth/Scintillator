TITLE:: TexPos
summary:: VGen providing geometry-specific texture coordinates for image sampling.
categories:: Quarks>Scintillator>VGens>Intrinsics
related:: Classes/NormPos, Classes/FragCoord, Classes/ImageBuffer, Classes/Sampler

DESCRIPTION::
TexPos provides a texture coordinate representing the position on the underlying rendered geometry (currently only a full-screen rectangle). Unlike vertex coordinates such as those provided by link::Classes/NormPos::, texture coordinates vary from [0, 1] with 0, 0 in the upper-left hand corner. When rendering the full-screen rectangle they can be thought of as link::Classes/FragCoord:: scaled to resolution-independent units. TexPos is most commonly seen as in input to the link::Classes/Sampler:: for rendering images.

CLASSMETHODS::

METHOD:: fr

strong::dimensions::
table::
## strong::input:: || strong::output::
## none || 2
::

INSTANCEMETHODS::
private::inputDimensions, outputDimensions

EXAMPLES::

code::
// This code snippet starts the server, loads the test image from the Scintillator
// image testing repository, and renders it to the entire quad using the TexPos
// VGen.
(
~o = ScinServerOptions.new;
~o.width = 400;
~o.height = 300;
~v = ScinServer.new(~o).boot;
)

(
~ib = ImageBuffer.read(path: "~/src/TestGoldImages/sourceMedia/molly.png".standardizePath);
)

(
~f = ScinthDef.new(\f, {
	Sampler.fr(~ib, TexPos.fr);
}).add;
)

(
~t = Scinth.new(\f);
)
::

image::TexPosA.png::

code::
// Molly doesn't appreciate being rendered in the wrong aspect ratio! It compromises
// some of her remarkable charisma. We need to adjust for two different aspect ratios,
// the aspect ratio of the screen and the aspect ratio of the image.
(
~t.free;
)

(
~f = ScinthDef.new(\f, {
	var screenAspect = ~o.width / ~o.height;
	var imageAspect = ~ib.width / ~ib.height;
	Sampler.fr(~ib,
		TexPos.fr * Vec2.fr(screenAspect, 1.0) / Vec2.fr(imageAspect, 1.0));
}).add;
)

(
~t = Scinth.new(\f);
)
::

image::TexPosB.png::
