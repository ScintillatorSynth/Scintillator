%{
// Generated file, please edit original file at src/base/Intrinsic.cpp.in
// TODO: lint generated file, copy diff back to this one.
#include "base/Intrinsic.hpp"

#include <cstring>

#if _MSC_VER
// Disable MSVC warning on generated code.
#pragma warning(push)
#pragma warning(disable: 4267)
#elif __GNUC__ || __clang__
#pragma GCC diagnostic push
#pragma GCC diagnostic ignored "-Wmissing-field-initializers"
#endif

namespace {

%}
%language=C++
%compare-strncmp
%struct-type
struct IntrinsicPair { const char* name; scin::base::Intrinsic intrinsic; };
%%
vFragCoord, scin::base::Intrinsic::kFragCoord
vNormPos,   scin::base::Intrinsic::kNormPos
pi,         scin::base::Intrinsic::kPi
position,   scin::base::Intrinsic::kPosition
sampler,    scin::base::Intrinsic::kSampler
time,       scin::base::Intrinsic::kTime
vTexPos,    scin::base::Intrinsic::kTexPos
%%

} // namespace

#if _MSC_VER
#pragma warning(pop)
#elif __GNUC__ || __clang__
#pragma GCC diagnostic pop
#endif

namespace scin { namespace base {

Intrinsic getIntrinsicNamed(const std::string& name) {
    const IntrinsicPair* pair = Perfect_Hash::in_word_set(name.data(), name.size());
    if (!pair) {
        return Intrinsic::kNotFound;
    }
    return pair->intrinsic;
}

} // namespace base

} // namespace scin
